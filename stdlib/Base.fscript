import stdlib/Base/List;
import stdlib/Base/IO;
import stdlib/Base/Math;
import stdlib/Base/Record;
import stdlib/Base/Maybe;
import stdlib/Base/Either;

-- General
id x = x;
f ° g = \x -> f (g x);
(∘) = (°);
const x y = x;
putLn = tclass "String" \x -> put (x `addList` "\n");
print = putLn ° show;
printP = putLn ° showP;
printOrExec x = if ((typeof x) == "IO") then x else print x;
putRaw = putLn ° debugRaw;
not x = if x then False else True;
(==) 2 10 += eq;
eq x y = not (ord x y);
toBool = not ∘ not;
flip f x y = f y x;
x |> f = f x;
x /= y = not (x == y);
(!=) = (/=);
x >> y = x >>= (const y);

is_ x t = ((typeof x) `eq` t) || (if ((typeof x) `eq` "Record") then ((get "Type" x) `eq` t) else False);

isT = flip is;

is x t = (is_ x t) || ((t `eq` "String") && isString x) || (t `eq` "ANY");

isString = all (\x -> x `is_` "Char");

-- Boolean
x && y  = if x then y else x;
x || y  = if x then x else y;
x <  y  = -1 == (ord x y);
x >  y  = 1 == (ord x y);
x <= y   = not (x > y);
x >= y   = not (x < y);


-- JSON compatibility
null = Null;
false = False;
true = True;

-- FClasses
tclass tl f = if (tl && (isString tl))
    then tclass [tl] f
    else if tl
        then let (HT t ts) = tl in
            \y -> if (y `is` t)
                then tclass ts (f y)
                else throw "FClass" ("Not Type " `addList` t) y
        else f;

fclass = \pl -> \f -> if (pl `is` "Function")
    then fclass [pl] f
    else if pl
        then let (HT p ps) = pl in
            \y -> if (p y)
                then fclass ps (f y)
                else throw "FClass" ("Does not match the predicate") y
        else f;


--TODO: Maybe could be (+)?
show 1 30 += tclass "String" \x -> ['"'] + x + ['"'];

show 1 20 += tclass "Num" showNum;

show 1 20 += tclass "Char" \x -> ("'" + [x] + "'");

show 1 20 += tclass "Bool" \x -> if x then "True" else "False";

show 1 20 += tclass "Function" (const "<Function>");

show 1 20 += tclass "Null" (const "Null");

show 1 0 += const "<Unknown Type>";

showP = \x -> showPInner x 0;

showPInner 1 0 += \x -> const (show x);

showInt = tclass "Num" \x -> let s = show x in if ('e' `elem` s)
then s
else takeWhile (\x -> x /= '.') s;

(+) 2 20 += tclass ["Num", "Num"] addNum;

(+) 2 10 += tclass ["Bool", "Bool"] \x y -> x || y;

(-) 2 20 += tclass ["Num", "Num"] subNum;

(-) 2 20 += tclass ["Bool", "Bool"] \x y -> if y then False else x;

(*) 2 20 += tclass ["Num", "Num"] mulNum;

(/) 2 20 += tclass ["Num", "Num"] divNum;

until = tclass ["Function", "Function", "ANY"] \p f x ->
    if p x then x
    else until p f (f x);